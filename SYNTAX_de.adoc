= Syntax zum WNF-Compiler
Doc Writer <lars[dot]langhans[at]gmx[dot]de>
v1.0
:toc:
:toclevels: 3


Die Syntax ist relativ offen, erstmal soll ein Grundgerüst
so weit stehen und jede Menge Tests zeigen, das der Compiler macht,
was man von ihm verlangt.
_"Schnelleren Code erzeugen, als compiliertes (Turbo)BASIC."_.
Dabei aber genauso einfach zu programmieren sein wie strukturiertes Basic.

== Die Syntax

Ich habe mir mal die Mühe gemacht, die Syntax in BNF aufzuspalten.
Als die erste Version des Compilers entstand, kannte ich lex und yacc noch nicht.
Trotzdem sollte die Sprache in
https://de.wikipedia.org/wiki/Backus-Naur-Form[BNF]
abbildbar sein.

link:winife.bnf[Backus Naur Form] zum WNF-Compiler

Eine Datei sieht grob immer wie folgt aus
```
(PROGRAM |INCLUDE [suffix:])name
[ LOMEM=<adresse>[,runad] ]
BLOCK
```

`(PROGRAM |INCLUDE [suffix:])name`:: Jede WNF-Datei startet entweder mit `PROGRAM name` oder `INCLUDE suffix:name`. Der `name` ist ohne Extension anzugeben, max. 8 Zeichen lang, unter diesem Namen
wird das Assembler-Programm abgelegt. Vorsicht, die Datei kann sehr groß werden >180kb ist möglich!

TODO: längere Namen supporten.

`PROGRAM name`:: besagt, es wird ein Program mit einer Main() Funktion erstellt.
Hier funktioniert LOMEM und INCLUDE am Ende.

`INCLUDE suffix:name`:: besagt, es werden nur Proceduren/Funktionen zu Assembler übersetzt,
LOMEM ist nicht erlaubt,
INCLUDEs am Ende sind auch nicht erlaubt. +
Es gibt keine Main() Funktion.
Das Ergebnis ist eine per "include" einbindbare INC-Datei,
die in ein PROGRAM-File eingebunden werden kann.
Alle Variablennamen werden mit 'suffix' versehen. Das könnte man vereinfachtes Datahiding nennen,
da das Programm nicht auf diese Variablen zugreifen kann.
Es sei denn, die Variable/Funktion oder Procedure startet mit '@'.
So lassen sich globale Variablen/Funktionen/Proceduren definieren,
die von außen aufgerufen werden können.

`[ LOMEM=<adresse>[,runad] ]`:: verpasst dem Programm eine andere Startadresse.
Ist kein LOMEM angegeben, ist die aktuelle Startadresse $4000, zudem gibt
 es extra einen Einstieg für Atari-(Turbo-)Basic,
dies ist LOMEM+3.
Soll das erstellte Programm von Basic gestartet werden, und ist an Adresse $4000 gebaut,
kann es von Basic mit `?USR($4003)` gerufen werden.
Dabei wird der Stack entsprechend richtig abgeräumt, das Programm kann fehlerfrei zurückkehren mit @exit().
Um ungefähr zu erfahren, wo das Basic Programm ended, kann
`?hex$(dpeek($90))` verwendet werden, es zeigt ungefähr auf das Ende bei TurboBasic. +
Vorsicht, der Wert schwankt leicht.
Ist halt nur ein grober Anhaltspunkt.
Wird noch `,runad` angehängt, wird RUNAD gesetzt, was dazu führt, das Programme vom DOS automatisch gestartet werden können. +
Werden Parameter in Atari-Basic übergeben, können diese Parameter über ein Array ausgelesen werden. +
`word array args[1] = @parameter` +
irgendwo vor der Hauptroutine, gibt den Zugriff auf die Parameter frei.
Dann kann in der Hauptroutine über args[0..n]
auf die Parameter zugegriffen werden.
Es findet keine Prüfung statt, ob der Parameter existiert. +
**Wichtig:** Da die Atari-Basic-Parameter auf dem Heap abgelegt werden,
sollten diese zur richtigen Nutzung zuerst in eigene Variablen kopiert werden.


`BLOCK`:: Das gesamte Programm besteht aus exakt einem Block.
Ein Block hat dann keine bis mehrere Proceduren/Functionen
und ein abschließendes Statement. Dann noch ein paar includes.
Fertig: Mehr kann der Compiler nicht, total einfach.

Ok, spalten wir das ganze etwas auf:

Am Anfang werden immer 3 Dateien includiert: +
- VARIABLE.INC (enthält viele Atari-Variablen) +
- HARDWARE.INC (fast alle Hardware-Adressen) +
- MACROS.INC (ein paar interne Macros) +
da Macros und wichtige Variablen vom Assembler
als erstes bekannt sein müssen.

* TODO: einige Macros nutzen Parameter, es gibt eine Möglichkeit
auf Anzahl der Parameter zu prüfen, mal einsetzen.

Am Ende können beliebig viele Dateien includiert werden.
Diese Dateien enthalten dann alle in Assembler
geschriebenen Hilfsroutinen,
wie @PRINT_STRING, @DECOUT, ...

* TODO: Herausfinden, warum die `.if .ref name` nicht funktioniert.

Ganz am Ende wird immer eine RUNTIME.INC mit includiert.
Diese wird nicht explizit mit angegeben.
Da es die letzte Datei sein muss. Setzt der Compiler das hier selbst.
In der RUNTIME.INC ist der Code für die rudimentäre Initialisierung.
Als letztes ist dort der Heap-Pointer untergebracht.

=== Block

Ein Block definiert globale Variablen,
beliebig viele Proceduren und oder Funktionen und
am Ende genau ein Statement.

`[ GLOBAL_VARIABLES * ]`::

`[ (PROCEDURE|FUNCTION) * ]`::

`STATEMENT`::

Mittlerweile ist der strikte Block etwas aufgeweicht,
es können vor jeder Procedure/Funktion globale Variablen
angelegt werden, auch vor dem letzen Statement.

=== GLOBAL_VARIABLES
Variablen nehmen Werte entgegen.
Aktuell werden 4 Arten unterstützt:

* Word -32768 - 32767
* Byte 0 - 255
* Int8 -128 - 127
* Uint16 0 - 65535

Byte und Word jeweils auch als Arrays.
Strings gibt es auch, das sind aber eigentlich byte arrays.

`byte name,name2`::  Eine Integer Variable mit 1 byte, durch Komma getrennt sind weitere Variablen möglich.

`byte COLPF2=710`::  Eine byte Integer-Variable dessen Adresse bei 710 liegt.

`byte COLPF2=@`::    Eine byte Integer-Variable dessen Adresse jetzt aus der HARDWARE.INC kommt.
Der Name dieser Variablen muss in der HARDWARE.INC vorhanden sein. Das funktioniert aktuell nur in PROGRAM-Dateien und nicht in den INCLUDE-Dateien.

**Achtung:** Mit dem `=` weist man einer Variablen dessen
Speicherort zu, nicht einen Wert.
Damit kann man dann hier später die Farbe vom Playfield 2 ändern,
also auf einem 8-bit Atari die blaue Farbe von Graphics 0.

`byte RAMTOP=@`:: weist einer Variable einen Speicherort zu. Hier RAMTOP (106). Diese Variable ist schon in der Datei VARIABLE.INC oder der Datei HARDWARE.INC definiert. Der Compiler kennt die Atari Variablen per default nicht. In den beiden genannten Dateien sind aber schon viele Adressen zugewiesen, allerdings nicht alle.
* TODO: In INCLUDE-Dateien funktioniert diese Art von Zuweisung aktuell nicht.

`word name,name2`::   Eine 16bit Integer Variable mit 2 Bytes, durch Komma getrennt sind weitere Variablen möglich.

`word SAVMSC=88`:: wie bei Bytes.
`word SAVMSC=@`:: wie bei Bytes.

`const VARIABLE=123` um den vielen "Magic Numbers" Herr zu werden, wurde eine simple Definition für Konstanten hinzugefügt.
Die Variable kann überall eingesetzt werden,
wo eigentlich Zahlen eingesetzt werden.
Hiermit vergibt man einen Namen.

==== Arrays
* Array Zugriff erfolgt mit eckigen Klammern, wie in Java/C es
sind nur eindimensionale Arrays möglich.
Der Einfachheit halber wird die Anzahl nicht geprüft.

`byte array name[anzahl bytes]`:: Ein Byte Array mit einer gewissen Anzahl
an Werten, 'anzahl bytes' gibt an, wie viele Bytes angelegt werden. Ist die Anzahl < 256, wird einfach der indirekte Zugriff
mit dem Y-Register verwendet.
Laden eines Wertes mit `LDA name,Y` oder Schreiben eines Wertes mit `STA name,Y` damit kann man sehr schnell auf den Speicher zugreifen.
Bei Anzahl >= 256 wird es als FAT_BYTE_ARRAY gehandhabt, der Zugriff erfolgt
über ein Macro und ist etwas langsamer, weil
die Position im Speicher erst berechnet werden muss.

`byte array dlist[0]=48160`::
Hier wird dem Array dlist die Adresse 48160 zugewiesen.
Ein Zugriff auf dlist[1] ist ein Zugriff der Adresse 48160+1.
Wenn man sich in Graphics 0 befindet und peek(106)=192 ist, dann liegt hier
die Displaylist und man kann darauf zugreifen und diese manipulieren.
Leider gibt es aktuell keine einfache Möglichkeit diesen Wert aus DPEEK(560) indirekt
auszulesen.

**Achtung:** Da intern keinerlei Array-Ränder geprüft werden,
muss die Länge eines Arrays nicht exakt angegeben werden.

`byte array name[2]`
legt irgendwo im Speicher 2 Bytes für das Array an.

`byte array direct[10]=[ 0,2,4,6,8,10,12,14,16,18 ]`::
Legt 10 Byte Werte direkt im Speicher ab.

`byte array hposp[1] = 53248` weißt dem Array hposp die Adresse 53248 zu. So kann per hposp[0] := 48 dem Player 0 eine neue X-Position zugewiesen werden.

`byte array direct[256]=[ ganz viele Werte ]`::
Legt ein Byte array an, dessen Zugriff über eine kleine
Berechnungsroutine läuft, damit kann man auf mehr als 256 Werte
schnell zugreifen.
Intern wird das als FAT_BYTE_ARRAY gehandhabt.

`byte array einString[1]=[ "Hallo Welt" ]`::
Ist eine Möglichkeit einen String im Speicher abzulegen.
Strings werden immer mit $FF terminiert.
Warum ausgerechnet $FF? Is so, fertig.

`string einString = ["Hallo Welt"]`::
Ist eine weitere Möglichkeit einen String im Speicher abzulegen.
Spart nur Tipparbeit, das Resultat ist ähnlich ein Byte Array.
Da es ein byte array ist funktioniert einString[0] auch hier.

`word array bigw[3]`::
Das ganze auch für word Werte (2 Byte)
Damit die word arrays auch etwas schneller arbeiten,
gibt es sog. Splitt-Arrays.

Dabei wird das word array in low byte array und high byte array aufgespalten.
Hier werden also intern ein byte array bigw_low und ein byte array bigw_high,
also eigentlich 2 byte arrays angelegt.
Der Zugriff erfolgt wie beim byte Array über den Index, schneller geht es nicht.
Man muss nur selbst dafür Sorge tragen den Index < 256 zu lassen.
----
bigw
bigw_low
  .byte 0,0,0
bigw_high
  .byte 0,0,0
----

Braucht man mal größere word arrays,
muss leider auf einen extrem langsamen indizierten Zugriff
umgeschaltet werden.
Also besser versuchen word arrays < 256 zu halten und
den Index-Zugriff verwenden.

`word array bigw[1] = 1234`::
Damit zeigt das Array bigw auf die Adresse 1234.
Der Zugriff erfolgt langsam über eine Berechnung der Adresse.

`word array bigw[@split] = @variable`::
Eine Zuweisung einer externen Variablen (@variable) an ein internes Splitt-Array.
Normalerweise ist das so nicht erlaubt, aber durch den Zusatz @split in der Array definition
kann es erzwungen werden. Dabei ist darauf zu achten, das die @variable_low und @variable_high
im Assembler Source definiert sein müssen. Sonst meckert der Assembler.

Damit man doch weiß, wie groß ein Array nun wirklich ist, erstellt
der Compiler zusätzlich zwei Konstanten mit dem Namen des Arrays+'_LENGTH' das den Wert in den eckigen Klammern wiederspiegelt und Name es Arrays+'_ELEMENTS' wie viele Elemente wirklich angelegt wurden.
Also hier die Konstanten `bigw_length` und `bigw_elements`.

Liste von Strings::
----
// Die einzelnen String
string eins=["eins"]
string zwei=["zwo"]

// list[] enthält jetzt die Strings
word array list[1] = [eins, zwei]
----
Zwei Strings, die in einer Liste abgelegt sind.
Über list[0] kommt man an den ersten String ("eins") heran.
So sind Listen von Strings möglich.
Ein Feature, das es in Atari-Basic nicht gibt.
Hier kann auf den Adressenprefix "adr:" verzichtet werden.
adr:eins, adr:zwei geht trotzdem und wird überlesen,
ist nur mehr Tipparbeit.

[source]
word array list[1] = ["eins", "zwei"]

Das direkte Angeben von Strings in Word-Listen funktioniert auch.

=== PROCEDURE
Eine Procedure definiert ein Unterprogramm ohne Rückgabewert,
das von irgendwo anders aufgerufen werden kann.
Es kann beliebig viele Variablen als Parameter entgegennehmen,
die übergebenen Variablen müssen global existieren.
Die aktuellen globalen Werte werden bei der Parameterübergabe
im Heap zwischengespeichert und am Ende wieder hergestellt.
Somit sind die an die Procedure übergebenen Variablen innerhalb
der Procedure dann lokal anzusehen.
Es ist möglich, weitere Variablen innerhalb einer Procedure als lokal zu markieren.
Es gibt in einer Procedure exakt ein Statement.

`[GLOBAL_VARIABLES *]`::

`PROCEDURE name(var, *) [LOCAL LOCAL_VARIABLES ]`::

`STATEMENT`::

Eine Procedure ist ein Unterprogramm,
es hat einen Namen und beliebig viele Parameter.
Die Parameter werden 'call by value' übergeben, auf dem Heap gesichert und
sind innerhalb der Procedure lokal, weitere lokale Variablen können
mit `LOCAL` name,... angelegt werden, auch diese Variablen werden auf dem Heap abgelegt.
Die Variablen müssen aber auch schon global existieren.
Proceduren können an beliebiger Stelle mit `RETURN wert` verlassen werden,
der Wert muss angegeben werden, wird aber nicht weiter ausgewertet.

`[GLOBAL_VARIABLES *]`::

`FUNCTION @name(var, *) [LOCAL LOCAL_VARIABLES ]`::

`STATEMENT`::

Eine Function ist ein Unterprogramm _mit_ einem Rückgabewert,
es hat einen Namen und wie Proceduren beliebig viele Parameter.
Die Parameter werden 'call by value' übergeben,
auf dem Heap gesichert und sind innerhalb der Funktion lokal,
weitere lokale Variablen können mit `LOCAL` name,... angelegt
werden, auch diese Variablen landen auf dem Heap.
Die Variablen müssen aber auch schon global existieren.
Funktion können an beliebiger Stelle mit `RETURN wert` verlassen werden,
der Wert muss angegeben werden und wird einer Variablen zugewiesen.
void Funktionen wie in C, gibt es nicht, dafür sind die Proceduren.
Funktionen können beliebig andere Funktionen aufrufen,
auch sich selbst, Rekursionen sind bis zu ~120 calls möglich,
da nur die Rücksprungadresse auf dem 6502-Stack landet und die Parameter auf einem eigenen Heap.

Globale Variablen dürfen vor jeder Procedure/Function definiert werden.
Diese sind auch erst ab dann gültig.
Das prüft aber nur der Compiler.

Jeder Funktions/Procedurename existiert doppelt, einmal so wie definiert und einmal mit '_' und der Anzahl der Parameter als 'i' Zeichen.
`PROCEDURE name(a,b) ...` erzeugt `name` und `name_ii`. Aufgerufen wird immer der Name mit dem Anhang, hier also name_ii. Der name ohne den Anhang wird gebraucht für z.B. dessen Adresse `adr:name` etc..


=== STATEMENT
Ein Statement ist das eigentliche Arbeitspferd im Compiler, es kann aber immer nur genau
ein Statement angegeben werden.

Allerdings ist `begin STATEMENT [STATEMENT]* end` auch ein Statement.

`begin STATEMENT [STATEMENT]* end`::
Mit `begin` startet ein Block-Container in dem
so lange Statements angegeben werden können, bis ein `end` kommt.
Nur so kann die Beschränkung auf genau ein Statement umgangen werden.

`if CONDITION then STATEMENT else STATEMENT`::

If-then-else mit dem berühmten https://en.wikipedia.org/wiki/Dangling_else[Dangling-else].
Ist die condition true, wird das 1. Statement ausgeführt,
sonst das Statement hinter dem else.


=== LOOPS

`while CONDITION do STATEMENT`::

Schleife um ein Statement.
Die Schleife wird ausgeführt, solange die Condition true ist.
Ist die Condition gleich false,
wird das Statement überhaupt nicht ausgeführt.

`repeat [STATEMENT]* until CONDITION`::
// weitere Schleife

Schleife um Statements (plural)!
Die Schleife wird solange ausgeführt, bis die Condition true ist.
Die Schleife wird min. einmal durchlaufen.
Zwischen `repeat` und `until` können beliebig viele Statements angegeben
werden.

`for var := EXPRESSION (to|downto) EXPRESSION [step EXPRESSION] do STATEMENT`::

`for` startet eine Zählschleife, die eine Variable `var` mit einem Startwert
initialisiert und das Statement solange wiederholt,
bis der Endwert erreicht oder überschritten ist.
Dabei wird die Variable bei `to` in jedem Durchlauf um 1 erhöht
und bei `downto` um 1 runtergezählt.
Bei der Angabe von `step` kann ein anderer Zählwert angegeben werden.

Eine For-Schleife `for i:=0 to 3 do Statement` hat 4 Durchläufe. 0, 1, 2, 3

**Vorsicht:** Man kann die Variable (hier i) im Statement anpassen,
das sollte man aber lassen.
Möchte man andere Steps haben, sollte ein `step EXPRESSION` mit angegeben werden.
Für eine dynamische Anpassungen zur Laufzeit sollte auf `while` oder `repeat` ausgewichen werden.

==== Das Problem mit dem Überlauf:

Eine Variable vom Typ `byte` kann nur die Werte von 0 bis 255 annehmen.
Die `for`-Schleife prüft beim Start, ob dieser Wert schon _überschritten_
ist, sollte das der Fall sein, wird die Schleife gar nicht erst
ausgeführt.
Beim Durchlaufen der Schleife mit eine Byte-Variablen von 0 bis 255,
tritt jetzt der Fall ein, das am Ende geprüft wird, ob die
Byte-Variable am Ende > 255 ist.
Der Fall kann aber nicht eintreten, da nach 255 eine 0 kommt
und somit die Schleife endloss läuft.
Endweder sollte dann der Type `word` verwendet werden, oder das Ende
auf 254 gesetzt werden.

Das gleiche tritt beim Runterzählen bis 0 auf.
`for var:=255 to 0 do ...` da eine Byte-Variable nicht < 0 sein kann, ist die Prüfung auf < 0 falsch und die Schleife läuft endloss.

Auch hier sollte auf den Type `word` umgestellt werden,
der Werte-Bereich von `word` ist -32768 bis 32767.

Bei Word tritt der Überlauf ab 32767 auf.

Deshalb gibt es in der for-Schleife auch noch die Typen:

* int8 mit dem Werte-Bereich von -128 bis 127
* uint16 mit dem Werte-Bereich von 0 - 65535

==== Ausbruch aus einer Schleife

`break`::

ermöglicht das Ausbrechen aus einer Schleife, es wird nur die innerste Schleife verlassen.


=== Zuweisungen

`VARIABLE := EXPRESSION`::

Einer definierten Variable wird das Ergebnis einer Expression zugewiesen.
Selbst einfache Berechnungen werden nicht vorberechnet, wie das z.B. in Java der Fall ist, dafür ist einfach kein Platz.
`2+2` bleibt `2+2` und wird nicht schon zu `4`.

* Die Variable bekommt das Ergebnis, das bei der Expression herauskommt zugewiesen.
* Es wird nicht geprüft, ob das Ergebnis in die Variable passt.
* Sollte das Ergebnis nur ein Byte sein, die Variable aber vom Type Word, so
wird das höherwertige Byte des Words auf 0 gesetzt. Ein Byte ist vorzeichenlos.
* Sollte das Ergebnis ein Word sein, die Variable aber nur vom Type Byte,
so wird das höherwertige Byte verworfen.
* Sollte die Variable vom Type Word sein, das Ergebnis aber int8, so wird eine
kleine Anpassung mit implementiert und das Vorzeichen in der Word Variable richtig zugewiesen.
* Expressions können beliebige Funktionsaufrufe enthalten.

`VARIABLE[EXPRESSION] := EXPRESSION`::
Wertzuweisung an ein Array, hier muss selbst darauf geachtet werden, das
die Expression innerhalb der Klammern in den Wertebereich der Definition
passt.
* Ist die Expression innerhalb der Klammer vom Type Word,
die Variable aber nur als `byte array[Zahl < 256]` definiert,
werden nur die Werte 0-255 akzeptiert.

Nochmals Vorsicht: die untere/obere Schranke, die ein Array aufzieht wird
nicht geprüft.
Greift man über den definierten Arraybereich, den man angegeben hat,
hinaus auf das Array zu, liefert es ggf. die nächste Variable. Oder
irgend welchen Code... Es ist halt nur eine 6502-CPU! MMU? Das kommt
erst später bei moderneren Prozessoren.


=== PROCEDURE_CALL / FUNCTION_CALL

`variable(GET_PARAMETER*)`::
Ein Procedure Call wird durchgeführt. Die aktuelle Adresse landet
auf dem 6502 Stack, dann werden sämtliche Parameter eingelesen,
und auf dem Heap IMMER als Word-Type (2 Byte) abgelegt.
dann wird per JSR die `variable` angesprungen.
Am Ende werden die Parameter aus dem Heap
wieder hergestellt.

Der erste Procedureaufruf ist ein Sonderfall, hier wird der Heap-Pointer
nicht manipuliert, das passiert erst, wenn eine Procedure eine weitere
Procedure aufruft.

Der Stack des 6502 wird nur vom JSR verwendet.
Sollte das Programm von Basic gestartet werden, sind ca. 112
rekursive Aufrufe möglich (getestet).
Bleiben wir in reinem Assembler ca. 120. (ungeprüft)

Ist die Variable noch nicht definiert, fängt aber mit `@` an, macht der
Compiler daraus einen `FUNCTION_CALL`.
Der Assembler ist dafür zuständig, den Namen aufzulösen.
Sollte das nicht klappen meldet das auch erst der Assembler.

Bei einem `FUNCTION_CALL` kann zusätzlich ein Wert per `RETURN wert` zurückgegeben werden.
Wie die Parameter in Assembler ausgefragt werden ist in link:TESTFUNC.INC[] beschrieben.

`x := @open(1,4,0,adr:file)`

oder

`x := @open(1,4,0,"D:TESTFILE")` Da wir Strings in Expressions haben, ist das hier erlaubt.

`@(funcname)(parameter)`::
Function Pointer Call.
Ein Procedure Call oder Function Call wird hier
indirekt über die Variable funcname ausgeführt.
Das muss der WORD Variablen funcname einfach
die Adresse der Funktion/Procedure übergeben werden.

`funcname := adr:funktionname` als Beispiel, wie funcname zugewiesen wird.


=== EXPRESSION

Eine `EXPRESSION` ist ein beliebiger mathematischer Ausdruck z.B. `2+2*2` das ergibt 6, weil hier
Punktrechnung vor Strichrechnung gilt. Desweiteren werden einfache Zahlen immer zu einem
Word, damit ist die Berechnung von _großen_ Zahlen einfacher.

Wir prüfen zuerst, ob es sich um einen String handelt und geben dann
die Adresse des Strings zurück.
Damit kann man überall wo Expressions erlaubt sind auch Strings einsetzen. Nur Rechnen mit
Strings ist nicht möglich. "aa"+"bb" ist falsch. 'a'+'b' ist 97+98 (195) weil Strings der
Länge 1 als Zahl aus den ASCII-Zeichen interpretiert werden. Deshalb gibt es keine 1 Byte langen Strings.

Intern wird der mathematische Ausdruck in eine UPN Notation umgesetzt.
Funktionen mit beliebigen Parametern können hier aufgerufen werden,
das Ergebnis der Funktion wird hier eingesetzt und zur weiteren Berechnung
genutzt.

Am Ende findet noch eine Optimierung statt,
es wird das ein oder andere push pull aus dem UPN Stack entfernt.
Bei einer bestimmten Konstellation wird statt adc ein inc verwendet.

* Multiplikation, Division und Modulo sind als Funktionen extern definiert, bei Verwendung
muss die Datei MATH_MUL.INC und oder MATH_DIV.INC includiert werden.

Eine für positive Werte funktionierende Optimierung ist die Möglichkeit über Shift.
Dazu muss der rechts stehende Faktor oder Quotient diese Werte (2,4,8,16,32,64,128,(256), 512,...)
enthalten.
Der Sonderfall hier ist 256. Bei der Multiplikation mit 256 wird einfach das untere Byte zum oberen Byte.
Bei der Division mit 256 wird einfach der obere Byte zum unteren Byte.

Neu ist jetzt für Multiplikationen das der rechts stehende Faktor,
per Star-Chain in shift/(add|sub) umgewandelt werden kann.
Gefunden in Dr.Dobbs No.12
Dadurch werden fast alle Multiplikationen mit festen Werten nochmals beschleunigt.

* HINWEIS: Die IMULT/IDIV Routinen arbeiten mit negativen Werten richtig, aber Shift nicht.

Für die schnellere Multiplikation gibt es die Datei VERYFASTMATH_MUL.INC,
darin wird die 8bit Multiplikation mittelx ((x+y)^2)/4 - ((x-y)^2)/4 durchgeführt
nebst großer Tabelle.

Als Tipp:: Verzichtet auf Multiplikationen/Divisionen und nutzt vorberechnete Tabellen/Arrays.

// Einfache Zahlen (`number`) werden immer als type word interpretiert, sonst
// funktionieren evtl. mult und div nicht richtig.


==== Expression in BNF

Wie die Expressions in BNF aktuell aussieht:

[Source]
 expression ::= (String | term (+ | - | ! | & | xor) term)
 term ::= (factor (* | / | mod) factor)
 factor ::= (number | -number | short-string | identifier | '(' expression ')' | '[' expression ']')
 identifier ::= (function-call '(' get_parameter ')' | 'adr:' variable-name | variable-name '[' factor | variable-name)
 variable-name ::= [a-zA-Z@][a-zA-Z0-9_@]*
 function-call ::= variable-name
 get_parameter ::= (nil | expression (',' expression)* )
 String ::= '\"' .* '\"'
 short-string ::= '\'' one-char '\''
 one-char ::= ?
 number ::= ( '%' binary | '#' quad | '$' hexadecimal | [0-9.]+ )
 binary ::= [0-1.]{8}
 quad ::= [0-3.]{4}
 hexadecimal ::= [0-9A-F.]+


* HINWEIS: Ein String fängt IMMER mit " an und endet mit ",
ein String mit single quotes ist nur noch als ein Zeichen erlaubt.
'A' wird zu 65, während "A" ein String ist mit einem Zeichen und eine Ende-Marke bekommt.

=== CONDITION

Eine mathematische Bedingung, es wird geprüft, ob ein Ausdruck kleiner, größer, ... als der andere Ausdruck ist.

[SOURCE]
 CONDITION ::= EXPRESSION CONDITION-SIGN EXPRESSION ((or|and) CONDITION)
 CONDITION-SIGN ::= ('=' | '==' | '<>' | '!=' | '<'|'>' | '<=' | '>=')

* es funktionieren nur `CONDITION and CONDITION and ...`
oder `CONDITION or CONDITION or ...`
wird and/or gemischt funktioniert es nicht!
Weil zu kompliziert, Klammern von and/or funktionieren hier auch nicht.

TODO: Hier nochmal etwas Gehirnschmalz zu investieren für eine bessere Verarbeitung.



**Deprecated** `assert(CONDITION, FEHLER-STRING)`::
Ist ein fest eingebautes Test-Statement.
Es wird die übergebene Condition geprüft und bei false
eine interne Zählvariable hochgezählt und der String
auf dem Editor-Fenster (E:) ausgegeben.
`@getAsserts()` liefert die Anzahl der internen Zählvariable.


== Fehlermeldungen

Der Compiler ist immer noch sehr rudimentär und gibt nur wenige Fehlermeldungen aus.
Manchmal ist leider genaueres Hinsehen nötig.
Das ist nicht schön, aber ein Relikt aus der Zeit, als der
Compiler noch in Turbo-Basic geschrieben war.


== Illegale Variablennamen
Der Compiler erzeugt keinen Binärcode, sondern Assembler-Source-Code.
Dieser muss erst noch durch einen Assembler wie den atasm.

Dieser Assembler hat im Umgang mit kurzen Variablen ein paar Problem.
Der Compiler verlängert diese Variablen einfach. Sollte man nur
in seinen eigenen Assembler-Routinen nicht vergessen.

* A
* ARR
* W

A ist jetzt möglich, wird intern zu 'A__', W wird intern zu W__


== technisches


=== Basic Nutzung
Dadurch, das aktuell nur die Zeropage-Register der internen Floating-Point-Routinen
verwendet werden, (212-255)
kann ein übersetztes WNF-Programm (LOMEM richtig setzen) gefahrlos in (Turbo-)Basic eingebunden
werden und kehrt auch sauber zum Basic zurück, dazu ist der LOMEM+3 Einstieg vorhanden,
Parameter vom Basic USR(adr, parameter1, parameter2, parameter-n...)
landen auf dem Heap.
Es sind beliebig viele Parameter erlaubt. (ungetestet).
Um einfach auf die Parameter von `USR(adr, parameter1, parameter2...)` zuzugreifen

`word array args[1] = @parameter`

definieren. Dann kann per `args[0]` auf Parameter1 zugegriffen werden.
`args[1]` ist Parameter2, usw.

Um einen Wert an das Basic zurückzugeben, einfach der internen Funktion
@exit den Wert übergeben `@exit(0)`.

In TurboBasic können fertig übersetzte Dateien mit `BLOAD "D:FILENAME"`
geladen werden.

=== Rekursion
Rekursive Procedure calls sind möglich, bis zu 112 Stufen, ab dann kann für nichts
mehr garantiert werden, da der Atari nur 256 Bytes Stack hat.
Parameter von Proceduren/Funktionen werden nicht auf dem Stack abgelegt
sondern auf einem eigenen Heap.
Da aber jede rekursive Funktion in eine imperative Funktion übersetzt werden kann,
sollte das kein Problem darstellen.

=== Compiler erzeugt Assembler
Der Compiler erzeugt kein fertiges Objekt-File sondern
Assembler-Source-Code, das erst noch durch einen Assembler in ein
Atari-Objekt-File übersetzt werden muss.

* Aktuell ist nur der atasm >=1.16 in der Lage das Assembler-File zu assemblieren.
* Der Assembler-Code ist _fast_ MAC/65 kompatibel, wurde für diesen halt mal erstellt.
** Nötig dazu: Anpassungen in den includes (# statt Quotes)
** Zeilennummern, die werden aktuell auch nicht eingefügt.

=== Fehlerhafter Code
Durch sehr viele Tests wurde sichergestellt,
das der erstellte Code _meistens_ läuft,
es wurden auch schon Spiele damit erstellt, um die Richtigkeit zu beweisen.
Auch ein Editor für Fonts wurde damit erstellt.

=== Nicht optimaler Code
Der erstellte Assembler-Source-Code ist alles andere als optimal.
Dafür ist er einfach gestrickt und es gibt halt Features
(proceduren, conditions, expressions, Strings, mult/div/mod etc.)
die es sonst nicht so einfach gibt.

* Expressions beherrschen Punkt vor Strichrechnung,
* Zahlenformate zur Basis 2(%), 4(#), 10 und 16($) (binär, quadrat, dezimal, hexadezimal).
Das Vierersystem ist bei der Atari-Grafik von Vorteil.
* Byte Arrays auf die einfach über Index-Register zugegriffen wird
* Word Arrays die bei einem Index < 257 in 2 Byte Arrays aufgesplittet werden.
Dann funktioniert auch hier der schnelle Index-Register-Zugriff.
* Proceduren mit Parametern und lokalen Variablen.
* Funktionen mit Parametern, lokalen Variablen und Rückgabewert.
Funktionen können innerhalb von Expressions genutzt werden.
* Strings können direkt übergeben werden.
* Es gibt String Arrays.
* Es gibt eine eigene Routine zur Ausgabe von Daten, Zahlen und Strings und Characters.
`@printf('Zahl:%d, String:%s, Char:%c\n', zahl, string, char)` macht das mal in Assembler...
* Zusätzlich existiert eine sehr schnelle Version `@printff(...)`.
Die direkt in eine vorher festgelegte Adresse die Daten schreibt.
* Eine kleine Grafikbibliothek die Linien und Kreis zeichnen kann.
Unterschiedlich schnelle Line Algorithmen.
* Unterschiedliche Multiplikations-Algorithmen
* Alles ist sehr einfach erweiterbar durch selbst geschriebene Assembler-Routinen.
* Es existiert ein Peephole-Optimizer, der einige wirklich suboptimale Konstrukte
des Compilers ausbügelt. Dieser ist aber nicht perfekt.

Das ist das Programmieren viel einfacher, als es in Assembler möglich ist,
dafür gibt es aber den "Compiler-Overhead".

Die Programme laufen vielleicht etwas langsamer und sind größer als handoptimierter Assembler,
aber nicht exorbitant größer und auch der Speed ist gegenüber Turbo-Basic signifikant.
Da ausschließlich Integervariablen verwendet werden.
Zudem können eigene Funktionen über includes eingebunden werden,
leider funktioniert im atasm das '.if .ref' nicht wie es der MAC/65 versteht,
deshalb gibt es viele kleine include-Dateien, diese müssen aktuell noch von Hand
eingebunden werden.

== Includes

Der Compiler includiert aktuell immer 3 Dateien am Anfang, noch bevor die
Origin-Start-Adresse gesetzt wird. (*=...)

* `VARIABLE.INC` sollte nur Variablen des OS enthalten
* `HARDWARE.INC` sollte nur Variablen der zugrunde liegenden Hardware enthalten.
Adressen ab $D000 für GTIA, Pokey, PIA, Antic,
* `MACROS.INC` enthält ein paar Macros für den Compiler
Diese 3 Includes sollten deshalb keinen eigenen Code enthalten.

Jetzt folgt der Code des eigentlichen Programms.

Am Ende die eigenen Includes für eigene Routinen. In beliebiger Reihenfolge.

Der Compiler includiert am Ende immer die `RUNTIME.INC`.
Diese enthält nur die absolut nötigsten Hilfsroutinen für den Betrieb.
Ganz am Ende wird der Heap_ptr gesetzt.
Meist reichen 256 Bytes für den Heap, je nachdem wie viele Unterroutinen man verwendet.
Ggf. muss es selbst ausprobiert werden, ob der Speicher reicht.

Da der gesamte Code nebst Runtime komplett offen liegt, kann auf wirklich fast
alles entsprechend Einfluß genommen werden.
Am Ende werden Assembler-Dateien erzeugt (*.ASM oder *.INC),
die nochmal durch einen Assembler zu Maschinen-Code übersetzt werden müssen.


=== Code-Größen
```
 procedure empty()
 begin
 end
```

Diese leere Procedure benötigt exakt 1 Byte. (RTS only)

```
 procedure oneByte(einByte)
 begin
 end
```

Summe: 38 Bytes

Wenn der HEAP_PTR nur 256 Bytes groß ist dann:

Summe: 32 Bytes

Diese leere Procedure bekommt einen Parameter übergeben und erzeugt:

* 11 Bytes, um den Parameter einByte in den Heap zu sichern und den Wert aus
dem Parameter zu holen.
* 7 Bytes, um den Heap anzupassen
* 7 Bytes, um den Heap am Ende zu restaurieren
* 6 Bytes, um den Parameter wieder herzustellen
* 1 Byte, das RTS

Jeder Byte-Parameter vergrößert eine Procedure um 17 Bytes.

```
 procedure oneWord(einWord)
 begin
 end
```

Summe 47 Bytes

Diese leere Procedure bekommt einen Parameter übergeben und erzeugt:

* 21 Bytes, um den Parameter einWord in den Heap zu sichern und den Wert aus dem Parameter zu holen.
* 7 Bytes, um den Heap anzupassen
* 7 Bytes, um den Heap am Ende zu restaurieren
* 11 Bytes, um den Parameter wieder herzustellen
* 1 Byte, das RTS

Jeder Word-Parameter vergrößert eine Procedure um 32 Bytes.

```
 procedure twoWords(einWord, zweitesWord)
 begin
 end
```

Summe 79 Bytes

Diese leere Procedure bekommt zwei Parameter übergeben und erzeugt:

* 21 Bytes, um den Parameter einWord in den Heap zu sichern und den Wert aus dem Parameter zu holen.
* 21 Bytes, um den Parameter zweitesWord in den Heap zu sichern und den Wert aus dem Parameter zu holen.
* 7 Bytes, um den Heap anzupassen
* 7 Bytes, um den Heap am Ende zu restaurieren
* 11 Bytes, um den Parameter zweitesWord wieder herzustellen
* 11 Bytes, um den Parameter einWord wieder herzustellen
* 1 Byte, das RTS

Jede Procedure mit Parametern kostet min. 14 Bytes, nur um den Heap-Ptr anzupassen, das
passiert inline, was etliche Takte (>24) für JSR/RTS spart.

```
 byte colbk=53274
 begin
   colbk:=$34
 end
```

Wird übersetzt zu:
```
 LDA #$34
 STA 53274
```
Das geht in Assembler nicht besser.

